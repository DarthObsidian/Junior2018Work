global proc string Rename(string $prefix, string $objectName, string $padding, string $suffix, int $num)
{
    string $temp = $num;
    if($padding != "none")
    {
        int $size;
        if($num > 9 && $num < 100)
        {
            $size = size($padding) - 1;
        }
        else if($num > 99)
        {
            $size = size($padding) - 2;
        }
        else
        {
            $size = size($padding);
        }
        for($i = 0; $i < $size; $i++)
        {
            $temp = "0" + $temp;
        }
    }
    
    if($prefix == "none" && $suffix == "none")
    {
        return $objectName + "_" + $temp;
    }
    else if($prefix == "none" && $suffix != "none")
    {
        return $objectName + "_" + $temp + "_" + $suffix;
    }
    else if($prefix != "none" && $suffix == "none")
    {
        return $prefix + "_" + $objectName + "_" + $temp;
    }
    else
    {
        return $prefix + "_" + $objectName + "_" + $temp + "_" + $suffix;
    }
}

global proc Selection(string $prefix, string $objectName, string $padding, string $suffix)
{
    string $selections[] = `ls -selection`;
    int $number = 1;
    for($objects in $selections)
    {
        string $newName = Rename($prefix, $objectName, $padding, $suffix, $number);
        rename $objects $newName;
        $number++;
    }    
}

proc Begin()
{
    string $prefix = `optionMenu -query -v Prefix`;
    string $suffix = `optionMenu -query -v Suffix`;
    string $padding = `optionMenu -query -v Padding`;
    string $objectName = `textFieldGrp -query -text Name`;
    Selection($prefix, $objectName, $padding, $suffix);
}

{
    window -width 200 -height 200 -title "Rename Objects";
        columnLayout;
            textFieldGrp -columnAlign 1 "left" -label "Name: " Name;
            optionMenu -width 100 -label "Prefix" Prefix;
                menuItem -label "R";
                menuItem -label "L";
                menuItem -label "none";
            optionMenu -width 100 -label "Suffix" Suffix;
                menuItem -label "Jnt";
                menuItem -label "Ctrl";
                menuItem -label "Grp";
                menuItem -label "none";
            optionMenu -width 100 -label "Padding" Padding;
                menuItem -label "#";
                menuItem -label "##";
                menuItem -label "none";
            button -label "Rename" -command "Begin";
    showWindow;   
}